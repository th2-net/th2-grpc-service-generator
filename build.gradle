plugins {
    id 'idea'
    id 'application'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    alias(libs.plugins.kotlin.jvm)
    alias(libs.plugins.shadow)
    alias(libs.plugins.th2.publish)
}

group = 'com.exactpro.th2'
version = release_version

repositories {
    mavenCentral()
}

configurations.configureEach {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
}

dependencies {
    api(libs.th2.bom)
    // Grpc and Protobuf
    api "com.google.protobuf:protobuf-java-util"
    api "io.grpc:grpc-stub"


    implementation "io.netty:netty-codec-http2"

    implementation "com.google.protobuf:protobuf-java"

    implementation "org.slf4j:slf4j-api"

    //Kotlin
    implementation(libs.kotlin.logging)

    implementation(libs.javapoet) {
        because("java code generation")
    }

    implementation "commons-io:commons-io"

    testImplementation(libs.junit.jupiter)
}

test {
    useJUnitPlatform()
}

java {
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    (options as StandardJavadocDocletOptions).addBooleanOption("html5", true)
}

application {
    mainClass = "com.exactpro.th2.service.generator.protoc.Main"
}

shadowJar {
    mergeServiceFiles()

    archiveFileName = project.name
    destinationDirectory = project.layout.buildDirectory.dir("artifacts")
}