plugins {
    id 'idea'
    id 'application'
    id 'java-library'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.jvm' version "1.8.22"
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'com.exactpro.th2.gradle.publish' version '0.0.6'
}

group = 'com.exactpro.th2'
version = release_version

kotlin {
    jvmToolchain(11)
}

repositories {
    mavenCentral()
    maven {
        name 'Sonatype_snapshots'
        url 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
    }
    maven {
        name 'Sonatype_releases'
        url 'https://s01.oss.sonatype.org/content/repositories/releases/'
    }

    configurations.configureEach {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
        resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
    }
}

dependencies {
    // Grpc and Protobuf
    api "com.google.protobuf:protobuf-java-util"
    api "io.grpc:grpc-stub"

    implementation "io.netty:netty-codec-http2"
    implementation "com.google.protobuf:protobuf-java"
    implementation "org.slf4j:slf4j-api"
    implementation 'io.github.microutils:kotlin-logging:3.0.5'
    implementation "com.squareup:javapoet:1.13.0" // For generation java code. License: Apache 2.0
    implementation "commons-io:commons-io"

    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
}

test {
    useJUnitPlatform()
}

javadoc {
    (options as StandardJavadocDocletOptions).addBooleanOption("html5", true)
}

mainClassName = "com.exactpro.th2.service.generator.protoc.Main"

shadowJar {
    mergeServiceFiles()

    archiveFileName = project.name
    destinationDirectory = "$buildDir/artifacts" as File
}